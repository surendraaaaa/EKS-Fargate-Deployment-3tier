# Create IAM policy for AWS Load Balancer Controller
resource "aws_iam_policy" "alb_policy" {
name = "AWSLoadBalancerControllerIAMPolicy-3tier"
description = "Policy for AWS Load Balancer Controller (generated by Terraform)"


policy = file("./iam_policy.json")
}


# Create IAM role for the service account (IRSA)
resource "aws_iam_role" "alb_sa_role" {
name = "eks-alb-controller-role-${var.cluster_name}"
assume_role_policy = data.aws_iam_policy_document.alb_assume_role.json
}


data "aws_iam_policy_document" "alb_assume_role" {
statement {
actions = ["sts:AssumeRoleWithWebIdentity"]
principals {
type = "Federated"
identifiers = [module.eks.oidc_provider_arn]
}


condition {
test = "StringEquals"
variable = "${module.eks.oidc_provider}:sub"
values = ["system:serviceaccount:kube-system:aws-load-balancer-controller"]
}
}
}


resource "aws_iam_role_policy_attachment" "alb_attach" {
for_each = { alb = aws_iam_policy.alb_policy.arn }
role = aws_iam_role.alb_sa_role.name
policy_arn = aws_iam_policy.alb_policy.arn
}


# Create Kubernetes service account with annotation for IRSA
resource "kubernetes_service_account" "alb_sa" {
metadata {
namespace = "kube-system"
name = "aws-load-balancer-controller"
annotations = {
"eks.amazonaws.com/role-arn" = aws_iam_role.alb_sa_role.arn
}
}
}


# Install AWS Load Balancer Controller via Helm
resource "helm_release" "aws_lb_controller" {
  name       = "aws-load-balancer-controller"
  repository = "https://aws.github.io/eks-charts"
  chart      = "aws-load-balancer-controller"
  namespace  = "kube-system"
  depends_on = [kubernetes_service_account.alb_sa]

  set = [
        {
            name  = "clusterName"
            value = var.cluster_name
        },
        {
            name  = "serviceAccount.create"
            value = "false"
        },
        {
            name  = "serviceAccount.name"
            value = kubernetes_service_account.alb_sa.metadata[0].name
        },
        {
            name  = "region"
            value = var.region
        },
        {
            name  = "vpcId"
            value = module.vpc.vpc_id
        }
    ]

}